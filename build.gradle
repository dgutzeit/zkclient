apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

sourceCompatibility = 1.6

group = 'com.maaii.cluster'
version = '0.2-SNAPSHOT'

zookeeperVersion = '3.4.3'
junitVersion = '4.8.1'
slf4jVersion = '1.6.4'
commonsIoVersion = '1.4'
mockitoVersion = '1.7'

repositories {
  mavenCentral()
}

configurations {
  provided
  testCompile.extendsFrom(provided)
}

dependencies {

  provided "org.apache.zookeeper:zookeeper:${zookeeperVersion}"

  compile "org.slf4j:slf4j-api:${slf4jVersion}"

  testCompile "junit:junit:${junitVersion}",
              "commons-io:commons-io:${commonsIoVersion}",
              "org.mockito:mockito-core:${mockitoVersion}"
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
}

tasks.idea.dependsOn(cleanIdea)
idea {
  module {
    scopes.PROVIDED.plus += configurations.provided
  }
}

task srcZip(type: Zip) {
  classifier = 'src'
  from sourceSets*.allSource
}

tasks.install.dependsOn(':build', ':srcZip')
